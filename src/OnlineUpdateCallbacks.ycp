/**
 * Module: 		OnlineUpdateCallbacks.ycp
 *
 * Authors:		Cornelius Schumacher <cschum@suse.de>
 *
 * Purpose: 		provides the Callbacks for the online update
 */
{
    module "OnlineUpdateCallbacks";

    textdomain "online-update";

    import "OnlineUpdateDialogs";
    import "PackageCallbacks";
    import "Popup";

    integer total_progress	= 0;

    /**
      Callback for YOU progress widget.
    */
    global define boolean ProgressCallback( integer num )
    ``{
//        y2milestone("ProgressCallback %1", num);

        if ( UI::WidgetExists( `id( `progress ) ) ) {
          UI::ChangeWidget( `id( `progress ), `Value, num );
        }

        symbol ret = (symbol)UI::PollInput();

        if ( ret == `abort || ret == `cancel ) {
            return false;
        }

	return true;
    }

    /**
      Callback for YOU patch progress widget.
    */
    global define boolean PatchProgressCallback( integer num /*, string pkg */ )
    ``{
        y2milestone("PatchProgressCallback %1", num);

        if ( UI::WidgetExists( `id( `patchprogress ) ) ) {
          UI::ChangeWidget( `id( `patchprogress ), `Value, num );
        }

        symbol ret =(symbol) UI::PollInput();

        if ( ret == `abort || ret == `cancel ) {
            return false;
        }

	return true;
    }

    global define string ErrorCallback( string type, string text,
                                        string details )
    ``{
//        y2internal( "ErrorCallback %1", text );

        if ( type == "skip" ) {
          symbol proceed = OnlineUpdateDialogs::SkipPopup( text, details );
          
          if ( proceed == `abort ) return "abort";
          else if ( proceed == `tryagain ) return "retry";
          else if ( proceed == `skip ) return "skip";
          else if ( proceed == `all ) return "skipall";
          else if ( proceed == `ok ) return "";
          else return "error";
        } else if ( type == "abort" ) {
          if ( OnlineUpdateDialogs::ConfirmAbortUpdate( `incomplete ) ) return "abort";
          else return ""; 
        } else {
          string msg = text;
          if ( size( details ) > 0 ) msg = msg + "\n" + details;
          Popup::Error( msg );
          return "error";
        }

	return "";
    }

    global define string MessageCallback( string type, list<map> patches )
    ``{
//        y2internal( "MessageCallback %1", type );

        if ( type == "preinfo" ) {
          boolean install = OnlineUpdateDialogs::MessagePopup( patches, true );
          if ( install ) return "";
          else return "skip";
        } else if ( type == "postinfo" ) {
          OnlineUpdateDialogs::MessagePopup( patches, false );
          return "";
        } else {
          y2error( "Wrong message type: %1", type );
          return "error";
        }

	return "";
    }

    global define void LogCallback( string text )
    ``{
//        y2milestone( "LogCallback %1", text );

        if ( UI::WidgetExists( `id( `log ) ) ) {
          UI::ChangeWidget( `id( `log ), `LastLine, text );
        }
    }

    /**
      Callback for executing YCP scripts in YOU.
    */
    global define boolean ExecuteYcpScriptCallback( string script )
    ``{
//        y2milestone("ExecuteYcpScriptCallback");

        return (boolean)WFM::call( script, [] );
    }

    /**
      Callback for showing script execution progress in YOU.
    */
    global define boolean ScriptProgressCallback( integer percent )
    ``{
//        y2milestone("ScriptProgressCallback");

        symbol ret = (symbol)UI::PollInput();

        if ( ret == `abort || ret == `cancel ) {
            return false;
        }

        return true;
    }

 
    /**
      Callback for starting download of a package.
     */
    global boolean StartProvide(string name, integer archivesize, boolean remote)
    {
	LogCallback( sformat( _("Retrieving %1..."), name ) );
	return true;
    }

     /**
      Callback for starting installation of a package.
     */
    global boolean StartPackage(string name, string summary, integer
                                        installsize, boolean is_delete)
    {
	LogCallback( sformat( _("Installing %1: \"%2\" "), name, summary ) );
	return true;
    }

   
     /**
      Callback for finishing an action in the log
     */
    global boolean FinishLine()
    {
	LogCallback( _("OK") + "\n" );
	return true;
    }


    /**
     * callback for 'package installed' action
     *
     *  return "" for ignore
     *  return "R" for retry
     *  return "C" for abort (not implemented !)
     */
    global string DonePackage (integer error, string reason) {
	string ret = PackageCallbacks::DonePackage (error, reason);
	if (ret == "")
	{
	    FinishLine ();
	    total_progress  = total_progress + 1;
	    if ( UI::WidgetExists( `id( `progress ) ) ) {
		UI::ChangeWidget( `id( `progress ), `Value, total_progress);
	    }
	}
	return ret;
    }

    /**
     * callback for 'package provided' action
     *
     *  return "" for ignore
     *  return "R" for retry
     *  return "C" for abort (not implemented !)
     */
    global string DoneProvide (integer error, string reason, string name) {
	string ret = PackageCallbacks::DoneProvide (error, reason, name);
	if (ret == "")
	{
	    FinishLine ();
	    total_progress  = total_progress + 1;
	    if ( UI::WidgetExists( `id( `progress ) ) ) {
		UI::ChangeWidget( `id( `progress ), `Value, total_progress);
	    }
	}
	return ret;
    }

    /**
      Constructor
    */
    global define void RegisterOnlineUpdateCallbacks()
    ``{
	y2milestone ( "OnlineUpdateCallbacks constructor" );

	Pkg::CallbackYouProgress ("OnlineUpdateCallbacks::ProgressCallback");
	Pkg::CallbackYouPatchProgress ("OnlineUpdateCallbacks::PatchProgressCallback");
	Pkg::CallbackYouError ("OnlineUpdateCallbacks::ErrorCallback");

	Pkg::CallbackStartProvide( "OnlineUpdateCallbacks::StartProvide" );
	Pkg::CallbackProgressProvide( "OnlineUpdateCallbacks::PatchProgressCallback" );
	Pkg::CallbackDoneProvide( "OnlineUpdateCallbacks::DoneProvide" );

	Pkg::CallbackStartPackage( "OnlineUpdateCallbacks::StartPackage" );
	Pkg::CallbackProgressPackage ("OnlineUpdateCallbacks::PatchProgressCallback");
	Pkg::CallbackDonePackage( "OnlineUpdateCallbacks::DonePackage" );

	Pkg::CallbackYouMessage ("OnlineUpdateCallbacks::MessageCallback");
	Pkg::CallbackYouLog ("OnlineUpdateCallbacks::LogCallback");
	Pkg::CallbackYouExecuteYcpScript ("OnlineUpdateCallbacks::ExecuteYcpScriptCallback");
        Pkg::CallbackYouScriptProgress ("OnlineUpdateCallbacks::ScriptProgressCallback");

        Pkg::CallbackMediaChange ("PackageCallbacks::MediaChange");
    }

    /*
     * Refresh all sources with autorefresh enabled.
     * This function is a temporary solution for bug #154990
     */
    global define void RefreshAllSources () {
	y2milestone("Refreshing all sources...");
	Pkg::SourceStartManager(true);
	list <map<string,any> > all_sources = Pkg::SourceEditGet();

	// There are no sources, nothing to refresh
	if (all_sources == nil || size(all_sources)<1) {
	    y2warning("No sources defined, nothing to refresh...");
	    return;
	}
	
	// Busy message popup
	UI::OpenDialog(`Label(_("Please wait...")));
	foreach (map<string,any> one_source, all_sources, {
	    integer source_id          = (integer) one_source["SrcId"]:nil;
	    boolean source_autorefresh = (boolean) one_source["autorefresh"]:true;
	    boolean source_enabled     = (boolean) one_source["enabled"]:true;
	    
	    if (source_id != nil && source_autorefresh == true && source_enabled == true) {
		y2milestone("Refreshing source: %1", source_id);
		Pkg::SourceRefreshNow(source_id);
	    }
	});
	UI::CloseDialog();
    }
}
