/**
 * Module:		OnlineUpdateCallbacks.ycp
 *
 * Authors:		Cornelius Schumacher <cschum@suse.de>
 *
 * Purpose:		provides the Callbacks for the online update
 */
{
    module "OnlineUpdateCallbacks";

    textdomain "online-update";

    import "OnlineUpdateDialogs";
    import "PackageCallbacks";
    import "Popup";

    integer total_progress	= 0;

    // if user aborted the installation
    boolean aborted	= false;

    // it might be useful to remember if download started...
    boolean downloading_delta	= false;

    /**
      Callback for YOU progress widget.
    */
    global define boolean ProgressCallback( integer num )
    ``{
        y2debug("ProgressCallback %1", num);

        if ( UI::WidgetExists( `id( `progress ) ) ) {
          UI::ChangeWidget( `id( `progress ), `Value, num );
        }
	if (aborted) return false;

        symbol ret = (symbol)UI::PollInput();

        if ((ret == `abort || ret == `cancel) &&
	    OnlineUpdateDialogs::ConfirmAbortUpdate (`incomplete)) {
            aborted	= true;
	    return false;
        }

	return true;
    }

    /**
      Callback for YOU patch progress widget.
    */
    global define boolean PatchProgressCallback (integer num)
    ``{
        y2debug("PatchProgressCallback %1", num);

        if ( UI::WidgetExists( `id( `patchprogress ) ) ) {
          UI::ChangeWidget( `id( `patchprogress ), `Value, num );
        }

	if (aborted) return false;

        symbol ret =(symbol) UI::PollInput();

        if ((ret == `abort || ret == `cancel) &&
	    OnlineUpdateDialogs::ConfirmAbortUpdate (`incomplete)) {
	    aborted	= true;
	    return false;
        }

	return true;
    }

    global define string ErrorCallback( string type, string text,
                                        string details )
    ``{
        y2milestone ("ErrorCallback %1, type: %2", text, type);

        if ( type == "skip" ) {
          symbol proceed = OnlineUpdateDialogs::SkipPopup( text, details );
          if ( proceed == `abort ) return "abort";
          else if ( proceed == `tryagain ) return "retry";
          else if ( proceed == `skip ) return "skip";
          else if ( proceed == `all ) return "skipall";
          else if ( proceed == `ok ) return "";
          else return "error";
        } else if ( type == "abort" ) {
          if ( OnlineUpdateDialogs::ConfirmAbortUpdate( `incomplete ) ) return "abort";
          else return "";
        } else {
          string msg = text;
          if ( size( details ) > 0 ) msg = msg + "\n" + details;
          Popup::Error( msg );
          return "error";
        }

	return "";
    }

    global void MessageCallback( string patchname, string patchsummary, string message )
    {
	// handle all messages as post (OK only)
        list<map> patches = [
	    $[
		"name" : patchname,
		"summary" : patchsummary,
		"postinformation": message,
	    ]
	];

        OnlineUpdateDialogs::MessagePopup( patches, false );
    }

    global define void LogCallback( string text )
    ``{
        y2milestone( "LogCallback %1", text );

        if ( UI::WidgetExists( `id( `log ) ) ) {
          UI::ChangeWidget( `id( `log ), `LastLine, text );
        }
    }

    /**
      Callback for executing YCP scripts in YOU.
    */
    global define boolean ExecuteYcpScriptCallback( string script )
    ``{
        y2milestone("ExecuteYcpScriptCallback");
        return (boolean)WFM::call( script, [] );
    }

    /**
      Callback for showing script execution progress in YOU.
    */
    global define boolean ScriptProgressCallback( integer percent )
    ``{
        y2milestone("ScriptProgressCallback");
        symbol ret = (symbol)UI::PollInput();

        if ( ret == `abort || ret == `cancel ) {
            return false;
        }

        return true;
    }

    /**
      Callback for starting download of a package.
     */
    global boolean StartProvide(string name, integer archivesize,boolean remote)
    {
	// progress log item (%1 is name of package)
	LogCallback( sformat( _("Retrieving %1..."), name ) );
        if (UI::WidgetExists (`id (`patchprogress)))
	{
	    UI::ChangeWidget (`id (`patchprogress), `Label,
		// progress bar label
		_("Package Progress"));
	    UI::ChangeWidget (`id (`patchprogress), `Value, 0);
	}
	return true;
    }

     /**
      Callback for starting installation of a package.
     */
    global boolean StartPackage(string name, string summary, integer
                                        installsize, boolean is_delete)
    {
	// progress log item (%1 is name of package, %2 is summary)
	LogCallback( sformat( _("Installing %1: \"%2\" "), name, summary ) );
        if (UI::WidgetExists (`id (`patchprogress)))
	{
	    UI::ChangeWidget (`id (`patchprogress), `Label,
		// progress bar label
		_("Package Progress"));
	    UI::ChangeWidget (`id (`patchprogress), `Value, 0);
	}
	return true;
    }

     /**
      Callback for finishing an action in the log
     */
    global boolean FinishLine()
    {
	// progress log item (=previous action finished correctly)
	LogCallback( _("OK") + "\n" );
	return true;
    }


    /**
     * callback for 'package installed' action
     *
     *  return "" for ignore
     *  return "R" for retry
     *  return "C" for abort (not implemented !)
     */
    global string DonePackage (integer error, string reason) {
	string ret = PackageCallbacks::DonePackage (error, reason);
	if (ret == "")
	{
	    FinishLine ();
	    total_progress  = total_progress + 1;
	    if ( UI::WidgetExists( `id( `progress ) ) ) {
		UI::ChangeWidget( `id( `progress ), `Value, total_progress);
	    }
	}
	return ret;
    }

    /**
     * callback for 'package provided' action
     *
     *  return "" for ignore
     *  return "R" for retry
     *  return "C" for abort (not implemented !)
     */
    global string DoneProvide (integer error, string reason, string name) {
	string ret = PackageCallbacks::DoneProvide (error, reason, name);
	if (ret == "")
	{
	    FinishLine ();
	    total_progress  = total_progress + 1;
	    if ( UI::WidgetExists( `id( `progress ) ) ) {
		UI::ChangeWidget( `id( `progress ), `Value, total_progress);
	    }
	}
	return ret;
    }

    /**
     * callback for start of delta download
     */
    global void StartDeltaDownload (string name, integer download_size) {

	downloading_delta	= true;
	// progress log item (%1 is name of delta RPM
	LogCallback (sformat(_("Downloading delta RPM %1"), name));
        if (UI::WidgetExists (`id (`patchprogress)))
	{
	    UI::ChangeWidget (`id (`patchprogress), `Label,
		// progress bar label
		_("Delta RPM Progress"));
	    UI::ChangeWidget (`id (`patchprogress), `Value, 0);
	}
    }

    /**
     * callback for delta download progress
     * @return boolean abort the download?
     */
    global boolean ProgressDeltaDownload (integer num) {

        y2internal ("ProgressDeltaDownload %1", num);
	return PatchProgressCallback (num);
    }

    /**
     * callback for problem during downloading delta
     */
    global void ProblemDeltaDownload (string description) {
	y2internal ("ProblemDeltaDownload: %1", description);
	LogCallback (
	    "\n" +
	    // progress log item (previous action failed(%1 is reason)
	    sformat(_("Failed to download delta RPM: %1"), description) +
	    "\n"
	);
    }

    /*
     * callback for start of applying delta rpm
     */
    global void StartDeltaApply (string name) {

	// progress log item (%1 is name of delta RPM)
	LogCallback (sformat(_("Applying delta RPM: %1"), name));
        if (UI::WidgetExists (`id (`patchprogress)))
	{
	    UI::ChangeWidget (`id (`patchprogress), `Label,
		// progress bar label
		_("Delta RPM Progress"));
	}
        if (UI::WidgetExists (`id (`patchprogress)))
	{
	    UI::ChangeWidget (`id (`patchprogress), `Label,
		// progress bar label
		_("Delta RPM Progress"));
	    UI::ChangeWidget (`id (`patchprogress), `Value, 0);
	}
    }

    /**
     * progress of applying delta
     * (cannot be aborted)
     */
    global void ProgressDeltaApply (integer num) {
	y2internal ("ProgressDeltaApply: %1", num);

        if ( UI::WidgetExists( `id( `patchprogress ) ) ) {
          UI::ChangeWidget( `id( `patchprogress ), `Value, num );
        }
    }

    /**
     * callback for problem during aplying delta
     */
    global void ProblemDeltaApply (string description) {
	y2internal ("ProblemDeltaAply: %1", description);
	LogCallback (
	    "\n" +
	    // progress log item (previous action failed(%1 is reason)
	    sformat(_("Failed to apply delta RPM: %1"), description) +
	    "\n"
	);
    }

    /**
     * callback for start of downloading patch
     */
    global void StartPatchDownload (string name, integer download_size) {

	// progress log item (%1 is name of delta RPM)
	LogCallback (sformat(_("Downloading patch RPM %1"), name));
        if (UI::WidgetExists (`id (`patchprogress)))
	{
	    UI::ChangeWidget (`id (`patchprogress), `Label,
		// progress bar label
		_("Patch RPM progress"));
	    UI::ChangeWidget (`id (`patchprogress), `Value, 0);
	}
    }

    /**
     * callback for problem during aplying delta
     */
    global void ProblemPatchDownload (string description) {
	y2internal ("ProblemPatchDownload: %1", description);
	LogCallback (
	    "\n" +
	    // progress log item (previous action failed(%1 is reason)
	    sformat(_("Failed to download patch RPM: %1"), description) +
	    "\n"
	);
    }

    // finish of download/application of delta or patch download
    global void FinishPatchDeltaProvide () {
	FinishLine ();
	total_progress  = total_progress + 1;
	if (UI::WidgetExists (`id (`progress)))
	{
	    UI::ChangeWidget (`id (`progress), `Value, total_progress);
	}
    }

    /**
      Constructor
    */
    global define void RegisterOnlineUpdateCallbacks()
    ``{
	y2milestone ( "OnlineUpdateCallbacks constructor" );

	Pkg::CallbackStartProvide( "OnlineUpdateCallbacks::StartProvide" );
	Pkg::CallbackProgressProvide( "OnlineUpdateCallbacks::PatchProgressCallback" );
	Pkg::CallbackDoneProvide( "OnlineUpdateCallbacks::DoneProvide" );

	Pkg::CallbackStartPackage( "OnlineUpdateCallbacks::StartPackage" );
	Pkg::CallbackProgressPackage ("OnlineUpdateCallbacks::PatchProgressCallback");
	Pkg::CallbackDonePackage( "OnlineUpdateCallbacks::DonePackage" );

	Pkg::CallbackResolvableReport ("OnlineUpdateCallbacks::MessageCallback");

	Pkg::CallbackYouProgress ("OnlineUpdateCallbacks::ProgressCallback");
	Pkg::CallbackYouPatchProgress ("OnlineUpdateCallbacks::PatchProgressCallback");
	Pkg::CallbackYouError ("OnlineUpdateCallbacks::ErrorCallback");
	Pkg::CallbackYouLog ("OnlineUpdateCallbacks::LogCallback");
	Pkg::CallbackYouExecuteYcpScript ("OnlineUpdateCallbacks::ExecuteYcpScriptCallback");
        Pkg::CallbackYouScriptProgress ("OnlineUpdateCallbacks::ScriptProgressCallback");

        Pkg::CallbackMediaChange ("PackageCallbacks::MediaChange");

	// delta download
	Pkg::CallbackStartDeltaDownload ("OnlineUpdateCallbacks::StartDeltaDownload");
	Pkg::CallbackProgressDeltaDownload ("OnlineUpdateCallbacks::ProgressDeltaDownload");
	Pkg::CallbackProblemDeltaDownload ("OnlineUpdateCallbacks::ProblemDeltaDownload");
	Pkg::CallbackFinishDeltaDownload ("OnlineUpdateCallbacks::FinishPatchDeltaProvide");

	// delta application
	Pkg::CallbackStartDeltaApply ("OnlineUpdateCallbacks::StartDeltaApply");
	Pkg::CallbackProgressDeltaApply ("OnlineUpdateCallbacks::ProgressDeltaApply");
	Pkg::CallbackProblemDeltaApply ("OnlineUpdateCallbacks::ProblemDeltaApply");
	Pkg::CallbackFinishDeltaApply ("OnlineUpdateCallbacks::FinishPatchDeltaProvide");

	// patch download
	Pkg::CallbackStartPatchDownload ("OnlineUpdateCallbacks::StartPatchDownload");
	Pkg::CallbackProgressPatchDownload ("OnlineUpdateCallbacks::ProgressPatchDownload");
	Pkg::CallbackProblemPatchDownload ("OnlineUpdateCallbacks::ProblemPatchDownload");
	Pkg::CallbackFinishPatchDownload ("OnlineUpdateCallbacks::FinishPatchDeltaProvide");
    }

    /*
     * Refresh all sources with autorefresh enabled.
     * This function is a temporary solution for bug #154990
     */
    global define void RefreshAllSources () {

	y2milestone("Refreshing all sources...");
	Pkg::SourceStartManager(true);
	list <map<string,any> > all_sources = Pkg::SourceEditGet();

	// There are no sources, nothing to refresh
	if (all_sources == nil || size(all_sources)<1) {
	    y2warning("No sources defined, nothing to refresh...");
	    return;
	}
	foreach (map<string,any> one_source, all_sources, {
	    integer source_id          = (integer) one_source["SrcId"]:nil;
	    boolean source_autorefresh = (boolean) one_source["autorefresh"]:true;
	    boolean source_enabled     = (boolean) one_source["enabled"]:true;
	    if (source_id != nil && source_autorefresh == true && source_enabled == true) {
		y2milestone("Refreshing source: %1", source_id);
		Pkg::SourceRefreshNow(source_id);
	    }
	});
	y2milestone ("... refreshing done");
    }
}
