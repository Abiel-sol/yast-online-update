/*
 *************************************************************
 *
 *     YaST2      SuSE Labs                        -o)
 *     --------------------                        /\\
 *                                                _\_v
 *           www.suse.de / www.suse.com
 * ----------------------------------------------------------
 *
 * Author:        Gabriele Strattner <gs@suse.de>
 *
 * Purpose:
 *
 * Modify:
 *
 * external function:
 *
 *
 *************************************************************

 $Id$

*/

{
    ////////////////////////////////////////////////////////////
    // Testmode, for example fo screenshots
    boolean test_mode    = lookup ( user_settings, "test_mode", false );
    boolean fake_mode    = lookup ( user_settings, "fake_mode", false );
  
    map last_status = $[];

    // get last update status from PKGINFO
    if ( !fake_mode )
    {
	last_status = PKGINFO(`ftpLastUpdateStatus() );
    }
    else
    {
	last_status = $["date":"12.10.2000", "days":"25", "status":"ok",
		       "patches":["patch-0-123", "recommended", "skdakdöak sdfsdfsdf", "30", "05.10.2000",
				 "sec-fix-988", "security", "salöda dsösld alsdäladä", "25", "12.10.2000",
				 "sec-fix-123", "security", "bbbbbbbb asdsfdf Cdsfdsf", "25", "12.10.2000" ]];
    }


    //////////////////////////////////////////////////////////////////////////////////////
    //                                  P O P U P S                                     //
    //////////////////////////////////////////////////////////////////////////////////////

    // None at the moment
    

    /////////////////////////////////////////////////////////////////////////////////////////////////////
    // MAIN:
    /////////////////////////////////////////////////////////////////////////////////////////////////////


    // create list for table
    list patch_input = [];
    integer index = 0;
    integer rec_no = 0;		// number of recommended patches
    integer sec_no = 0;		// number of security patches
    
    list patch_list = lookup( last_status, "patches", [] );

    while (index < size(patch_list) )
    {
	patch_input = add( patch_input,
			   `item(`id(select(patch_list, index)), select(patch_list, index+3),
				 select(patch_list, index), select(patch_list, index+2)) );
	if ( select(patch_list, index+1) == "recommended" )    		rec_no = rec_no+1;
	else if ( select(patch_list, index+1) == "security" )    	sec_no = sec_no+1;
	
	index = index+5;
    };

    list info_input = [];

    info_input = add( info_input, `item(`id(2), UI(_("Days since update:")), lookup(last_status, "days", "--")) );
    info_input = add( info_input, `item(`id(3), UI(_("Status of last update:")), lookup(last_status, "status", "--" )) );
    info_input = add( info_input, `item(`id(3), UI(_("Number of recommended patches:  ")), rec_no ));
    info_input = add( info_input, `item(`id(3), UI(_("Number of security patches:  ")), sec_no ));
     
    term contents =
	// main dialog: 
	`VBox(
	      `Left(`Label(_("Last update informations"))),
	      `VWeight( 3,`Table(`id(`info_table),`opt(`hvstretch), 
				 `header( _("Description"), _("Data") ),
				 info_input
				 )
			),
	      `VSpacing(0.2),
	      `Left(`Label(_("Informations about all installed patches"))),
	      `VWeight( 4,`Table(`id(`patch_table),`opt(`hvstretch), 
				 `header( _("Days"), _("Patch"), _("Description") ),
				 patch_input
				 )
			),
	      `Left( `PushButton(`id(`info), `opt(`notify), _("&Show patch information")) ),
	      `VSpacing( 0.2 )
	      );
    
    string help_text = "";

    // helptext  "main dialog online_update" 
    string help_part1 =  UI(_("<p>
This dialog shows detailed informations about the
last package update and about all patches
installed on your system.
</p>"));
    string help_part2 =  UI(_("<p>
More detailed informations about a special patch can
be obtained. Select first the patch in list and then
click on button <b>Show Patch Info</b>.
</p>"));

    help_text = help_part1 + help_part2;
    
     // using SetWizardContents (define in online_update.ycp)
     UI(`SetWizardContents("Detailed Informations ", contents, help_text, Args(0), Args(1) ));

      
   /////////////////////////////////////////////////////////////////////////////////////////
   ////  Loop for User Input ....
   /////////////////////////////////////////////////////////////////////////////////////////
    symbol ret = `next;

    repeat
    {
        ret= UI(`UserInput());

	if ( ret == `info )
	{
	    string patch = UI(`QueryWidget(`id(`patch_table), `CurrentItem ) );
	    UI(`ShowPatchInfo( patch ));
	}
        else if (ret == `next)
        {

	}
	else if ( ret == `abort  && UI(`ConfirmAbortUpdate(`painless)) )
	{
	    return `abort;
	}
	else if ( ret == `back)
	{

	}

  } until (ret == `next || ret == `back || ret == `cancel);
    
   return ret;
}



