/*
 *************************************************************
 *
 *     YaST2      SuSE Labs                        -o)
 *     --------------------                        /\\
 *                                                _\_v
 *           www.suse.de / www.suse.com
 * ----------------------------------------------------------
 *
 * Author:        Gabriele Strattner <gs@suse.de>
 *
 * Purpose:	start module for you update
 *		display last update info
 *		possibility to choose automatic or manual mode
 * Modify:
 *
 * external function:
 *
 *
 *************************************************************

 $Id$

*/

{
  textdomain "online_update";
   
  boolean test_mode    = lookup ( user_settings, "test_mode", false );
  boolean business = false;  
  
  // constant settings
  include "suseservers.ycp";

  // you_server_map = $[ "server" : [ "path", "type" ], ... ]

  string rootpath = "/";
  string yastpath = "/var/lib/YaST";
  string patchpath = "/var/lib/YaST/patches";
  string serverdescr_file = "suseservers.txt";  


  /////////////////////////////////////////////////////////////////////////////////////
  // MAIN:
  /////////////////////////////////////////////////////////////////////////////////////

  
  map last_status = $[];
  string default_language = "en";

  map lang2yast1 = SCR::Read (.target.yast2, "lang2yast1.ycp");  
  
  string language 	= lookup( user_settings, "language", default_language );
  boolean cd_update 	= lookup( user_settings, "cd_update", false );
  string long_language 	= lookup(lang2yast1, language, "english");  
  
  string architecture 	= lookup( user_settings, "architecture", "i386" );

  list you_server_list = [];

  // initialize You Agent
  UI::OpenDialog(`opt(`decorated ),`Label(_("Initializing for FTP/HTTP update. One moment please...")));

  any retval = SCR::Execute(.you.setEnvironment, $[ "language":long_language,
						  "rootpath":rootpath,
						  "architecture":architecture,
						  "yastpath":yastpath,
						  "patchpath":patchpath] );

  y2debug( "ONLINE: Return  .you.setEnvironment() %1", retval );
  UI::CloseDialog();

  string reloadServer =  SCR::Read(.rc.system.YAST2_LOADFTPSERVER);
  y2milestone ( "YAST2_LOADFTPSERVER = %1", reloadServer );

  // Check wether YOU runs on a business product

  if ( SCR::Read(.you.isBusiness) )
  {
      user_settings = add( user_settings, "business", true );
      business = true;
      serverdescr_file = "suseservers_http.txt"; 
  }
  
  if ( reloadServer != "no" )
  {

      // getting you-server-description from www.suse.de
      SCR::Execute(.target.bash,"/bin/cp /etc/suseservers /etc/suseservers.bak" );
      string command = "/usr/bin/wget -q --output-document="+
	  "/etc/suseservers --tries=2 --timeout=5 http://www.suse.de/de/support/download/"
	  + serverdescr_file;
      if ( SCR::Execute(.target.bash, command) != 0  )      
      {
	  y2error("command: %1", command);
	  SCR::Execute(.target.bash,
			"/bin/cp /etc/suseservers.bak /etc/suseservers" );
      }
      else
      {
	  y2milestone("command %1 ok", command);
      }
      SCR::Execute(.target.remove,"/etc/suseservers.bak" );	
  }

  map you_server_map = read_suseservers ();

  string you_server =   lookup(user_settings, "you_server", "" );
  string you_serverkind =   lookup(user_settings, "you_serverkind", "" );  
  string default_server = "";
  string default_serverkind = "";
  
  // default server is first entry in file /etc/suseservers (instead of "ftp.suse.com")
  if ( you_server == "" )
  {
      default_server = read_first_suseserver();
      default_serverkind = read_first_suseserverkind();
  }

  string you_dir =  lookup(user_settings, "you_dir", "" );

  // look for default directory in server map (instead of "/pub/suse")
  if ( you_dir == "" )
  {
      you_dir = select( lookup(you_server_map, you_server, ["",""]), 0 );
  }
  if ( you_serverkind == "" )
  {
      you_serverkind = select( lookup(you_server_map, you_server, ["",""]), 1 );
  }  
  y2milestone("YOU server: %1, directory: %2", you_server, you_dir );
  
  // insert local server like CD, NFS ....
  if ( you_server != "" && you_server != default_server )
  {
      list you_dir_list = [];
      you_dir_list = add ( you_dir_list, you_dir );
      you_dir_list = add ( you_dir_list, you_serverkind );      
      you_server_map = add(you_server_map, you_server, you_dir_list );
  }
    
  foreach(`server, `info, you_server_map, ``{
      you_server_list = add(you_server_list, server);
  });


  if (!retval)
  {
      // Message popup: yast2 component responsible for the you update cannot be initialised.
      // This does not mean, connection failed to you server, but any other error occured.
      UI::MessagePopup(_("Initialization of FTP/HTTP server failed:
internal error.
"));
      return `abort;
  }
  
  // get last update status from YOU agent
  last_status = SCR::Read(.you.lastUpdateStatus);

  y2debug("ONLINE: last update status: %1", last_status );

 integer num = tointeger( lookup( last_status, "days", "0" ));
 string last_update = sformat( _("Last update was executed\n %1 day ago.", "Last update was executed\n %1 days ago.", num), num );
  
  // layout main dialog 

  term contents =
      `VBox(
	    `VSpacing( 0.3 ),
	    `HBox(`HWeight(1, `Empty()),
		  `HWeight(8, `Frame (_("Last update information"),
				      `VBox(`VSpacing(0.8),
					    `HBox(`HSpacing(0.8),`Left(`Label(last_update)),
						  `HSpacing(2.0),
						  `PushButton( `id(`details), `opt(`notify), _("&Details"))
						  ),
					    `VSpacing(0.8)
					    )
				      )),
		  `HWeight(1, `Empty())
		  ),
	    `VSpacing(1.0),
	    `HBox(`HWeight(1, `Empty()),
		  `HWeight(8, `Frame (_("Choice of update mode"),
				      `RadioButtonGroup(`id(`choice),
							`VBox(
							      `VSpacing(0.3),
							      `HBox(`HSpacing(0.8),
								    `Left(`RadioButton(`id(`automatic),`opt(`notify),
										       _("Automatic &Update"), false) )
								    ),
							      `VSpacing(0.3),
							      `HBox(`HSpacing(0.8),
								    `Left(`RadioButton(`id(`manual),`opt(`notify),
										       _("&Manual Update") ,  false))
								    ),
							      `VSpacing(0.3) 
							      )
							)
				      )),
		  `HWeight(1, `Empty())
		  ),
	    `VSpacing(1.0),	      
	    `HBox(`HWeight(1, `Empty()),
		  `HWeight(8, `Frame (_("Choice of installation source"),
				      `RadioButtonGroup(`id(`choice),
							`VBox(
							      `VSpacing(0.3),
							      `HBox(`HSpacing(1.2),
								    `Left(`ComboBox(`id(`you), _("&Installation source"), you_server_list )),
								    `PushButton( `id(`expert), `opt(`notify), _("&Expert"))
								    ),
							      `VSpacing(0.3) 
							      )
							)
				      )),
		  `HWeight(1, `Empty())
		  ),
	    `VSpacing( 0.5 )
	    );
    
  string help_text = "";

  // helptext dialog online update start screen 
  string help_part1 =  UI(_("<p>SuSE Online Update is the easy way to get all recommended
patches and security fixes from a SuSE ftp/http server.<br>
<b>Automatic Update</b> will connect to the server,
fetch the files, and install the patches.</p>
"));
    
  string help_part2 =  UI(_("<p>
<b>Manual Update</b> will display all available patches
and you can choose which patches should be installed.<br>
Information about the last update will be shown
if you click on the <b>Details</b> button.
</p>"));

  string help_part3 =  UI(_("<p>After clicking <b>Expert</b>, select a local
installation source instead of an FTP/HTTP server or select 
another FTP/HTTP server.</p>
"));
    
    
  help_text = help_part1 + help_part2 + help_part3;

  if ( lookup( user_settings, "you_auto_install", false ) )
  {
      // auto mode; install patches which are currently on the server
      // Do not use internet
      
      return `next;      
  }  

  if ( lookup( user_settings, "you_auto_get", false ) )
  {
      // auto mode; get patch descriptions and packages, but do not install patches
      if ( size ( you_server_list ) <= 0 )
      {
	  y2error ( "No serverlist available" );
	  return `abort;
      }
      string you_server = read_first_suseserver(); 
      user_settings = add(user_settings, "you_server", you_server);

      string you_dir = select( lookup(you_server_map, you_server, []), 0 );
      user_settings = add(user_settings, "you_dir", you_dir);

      string you_serverkind = read_first_suseserverkind(); 
      user_settings = add(user_settings, "you_serverkind", you_serverkind);

      any retval = SCR::Execute( .you.setServer, $["kind":you_serverkind, "name":you_server, "path":you_dir]);
		
      if (retval == nil || retval == false)
      {
	  return `abort;
	  y2error("ONLINE: .you.setServer( %1, %2 %3) ): %4",
		  you_server, you_dir, you_serverkind, retval );
      }
      y2debug("ONLINE: .you.setServer( %1, %2 %3) ): %4",
	      you_server, you_dir, you_serverkind, retval );
      
      return `next;      
  }  
  
  if ( !cd_update )
  {
      UI::SetWizardContents(_("Welcome to SuSE Package Update"), contents, help_text, Args(0), Args(1) );

      UI::ReplaceWizardAbortButton( `PushButton(`id(`abort), _("&Abort Update")));
  }
  else
  {
      // write settings for CD update and return `next
      user_settings = add (user_settings, "you_auto", false);
      user_settings = add(user_settings, "you_server", "CD" );
      user_settings = add(user_settings, "you_serverkind", "CD" );
      user_settings = add(user_settings, "you_dir", "" );            

      any retval = SCR::Execute( .you.setServer, $["kind":you_serverkind, "name":you_server, "path":you_dir]);
      y2debug("ONLINE: .you.setServer( %1, %2 %3) ): %4",
	      you_server, you_dir, you_serverkind, retval );
      
      return `next;
  }
  

  if ( lookup(user_settings, "you_auto", false ) )
  {
      UI::ChangeWidget(`id(`automatic), `Value, true );
      UI::ChangeWidget(`id(`manual), `Value, false );
  }
  else
  {
      UI::ChangeWidget(`id(`automatic), `Value, false );
      UI::ChangeWidget(`id(`manual), `Value, true );
  }

  if ( you_server != "" )
  {
      UI::ChangeWidget(`id(`you), `Value, you_server );
  }

  //  Loop for User Input ....

  symbol ret = `next;

  repeat
      {
	  ret= UI::UserInput();

	  if ( UI::QueryWidget(`id(`automatic), `Value ) ) 
	  {
	      user_settings = add (user_settings, "you_auto", true);
	  }
	  else
	  {
	      user_settings = add (user_settings, "you_auto", false);
	  }

	  // add you_server to user_settings
	  string you_server =  UI::QueryWidget(`id(`you), `Value );
	  user_settings = add(user_settings, "you_server", you_server);
	
	  if (ret == `next)
	  {

	      you_dir = select( lookup(you_server_map, you_server, []), 0 );
	      user_settings = add(user_settings, "you_dir", you_dir);
	      you_serverkind = select( lookup(you_server_map, you_server, []), 1 );
	      user_settings = add(user_settings, "you_serverkind", you_serverkind);

	      any retval = SCR::Execute( .you.setServer, $["kind":you_serverkind, "name":you_server, "path":you_dir]);
	      
	      y2debug("ONLINE: .you.setServer( %1, %2 %3) ): %4",
	      you_server, you_dir, you_serverkind, retval );
	      
	      if (retval == nil || retval == false)
	      {
		  UI::DisplayMsgYou(_("Initialization failed. Try again.
If failure continues, choose
another SuSE FTP/HTTP server.\n"));
		  ret = `again;
	      }
	      else
	      {
		  // Show popup to get the registration and password in case of business product
		  // and no local installation
		  if ( business
		       && you_serverkind != "CD"
		       && you_serverkind != "Harddisk"
		       && you_serverkind != "Net" )
		  {
		      // get, check and possibly save the registration and password
		      boolean success = CheckAuthorization( );

		      if ( !success )
		      {
			  return `abort;
		      }
		  }
	      }	      
	  }
	  else if ( ret == `details )
	  {
	      any retval = CallFunction(`online_update_details( true, false ) );
	      if ( retval == `abort )
		  return `abort;
	    
	      ret = `again;
	  }
	  else if ( ret == `expert )
	  {
	      you_dir = select( lookup(you_server_map,
				       you_server, []), 0 );
	      you_serverkind = select( lookup(you_server_map,
					      you_server, []), 1 );
	    
	      user_settings = add ( user_settings, "you_dir", you_dir );
	      user_settings = add ( user_settings, "you_serverkind", you_serverkind );	      
	      user_settings = add ( user_settings,"post_install", false );
	      any retval = CallFunction(`inst_source( true, true ) );
	      if ( retval == `abort )
		  return `abort;

	      you_server = lookup ( user_settings, "you_server", "" );
	      you_serverkind = lookup ( user_settings, "you_serverkind", "" );	      
	      you_dir = lookup ( user_settings, "you_dir", "" );
	      
	      retval = SCR::Execute( .you.setServer, $["kind":you_serverkind, "name":you_server, "path":you_dir]);
      
	      y2debug("ONLINE: .you.setServer( %1, %2 %3) ): %4",
		      you_server, you_dir, you_serverkind, retval );		
	      if (retval == nil || retval == false)
	      {
		  UI::DisplayMsgYou(_("Initialization failed. Try again.
If failure continues, choose
another SuSE FTP/HTTP server.\n"));
	      }
	      ret = `again;
	  }
	  else if ( ret == `back)
	  {

	  }
	  else if ( ret == `abort && UI::ConfirmAbortUpdate(`painless) )
	      return  `abort;
	
      } until (ret == `back || ret == `next || ret == `again);

  y2debug("Return START: %1", ret );
    
  return ret;
}



