/*
 *************************************************************
 *
 *     YaST2      SuSE Labs                        -o)
 *     --------------------                        /\\
 *                                                _\_v
 *           www.suse.de / www.suse.com
 * ----------------------------------------------------------
 *
 * Author:        Gabriele Strattner <gs@suse.de>
 *
 * Purpose:	Dialog displays a list with all available patches.
 *		The user can select or deselect patches for download.
 *		Patches marked with "X" will be installed afterwards,
 *		patches marked with "G" will be downloaded, but not
 *		installed because they are not relevant for the system.
 *		
 * Modify:	
 *
 * external function:
 *
 *
 *************************************************************

 $Id$

*/

{
    // Testmode
    boolean test_mode    = lookup ( user_settings, "test_mode", false );
    
    // map ftp_patches contains information about all available patches, e.g.
    //
    // $["patch-001":["recommended", "Patch for NFS server", "234", "X"],
    //   "patch-002":["security", "Noch ein weiterer patch", "565", " "]];
    //
    // status information from all patches must be stored, because if the user
    // deselects a patch and selects it again we must know whether it was "X" or "G"
    map all_patches = lookup( user_settings, "ftp_patches", $[] );
    y2debug("ONLINE: %1", all_patches );
    
    // if the user doesn't do any changes, get all patches
    map get_patches = all_patches;
    
    //////////////////////////////////////////////////////////////////////////////////////
    //  Popups and Defines			                                        //
    //////////////////////////////////////////////////////////////////////////////////////

    // update map get_patches and change status in list

    define UpdateGetPatches ( string pname, string pstatus ) ``{

	maplist ( `patch, `value,  get_patches, ``(
	              {  if ( patch == pname )
		      {
			  UI( `ChangeWidget(`id(`patch_table), `Item( patch, 0 ), sformat( " %1 ", pstatus )) );
			  get_patches = add( get_patches, patch, [select(value, 0), select(value,1), select(value,2 ), pstatus] );
		      }
		      }));
    };
    
    /////////////////////////////////////////////////////////////////////////////////////////////////////
    // MAIN:
    /////////////////////////////////////////////////////////////////////////////////////////////////////

  
    
    // create table input, e.g.
    // [`item(`id(patch-001), " X ", "recommended", "patch1", "356", "Patch for NFS server"),
    //  `item(`id(patch-002), "   ", "security", "patch2", "2560", "Noch ein weiterer patch") ];
    //
    list table_contents = maplist( `key, `value, all_patches,
				   ``(`item(`id(key),
					    sformat( " %1 ", select(value, 3) ),
					    select(value,0),
					    key,
					    my_size_text( tointeger(select(value,2))),
					    select(value,1))
				      )
				   );

    term contents =
	    // main dialog: 
	`VBox(
	      `VSpacing( 0.2 ),
	       `Table(`id(`patch_table),`opt(`notify, `hvstretch), 
		      `header( " ", _("Mode"), _("Patch"), _("Size"), _("Description") ),
		      table_contents
			      ),
	      `HBox(`Left(`PushButton(`id(`apply), _("&Apply"))),
		    `Left(`PushButton(`id(`info), _("&Show description")))
		    ),
	      `VSpacing( 0.2 )
	      );
    
    string help_text = "";

    // helptext  "main dialog online_update"
    string help_text = "";
    
    string help_part1 =  UI(_("<p>This dialog shows all patches which are
available for download on SuSE ftp server.<br>
Mode \"recommended\" means you should install the patch.
\"security\" is a security patch and it is highly recommended to install it.
</p>"));
    string help_part2 =  UI(_("<p>Meaning of the status flags:</p>
<p>
<b>X</b>: Patches concerning your installation are preselected.
They will be downloaded and installed on your system.
If you don't want a certain patch, double
click on line to deselect it (or selcect line and use Crtl A).
</p>"));
    string help_part3 =  UI(_("<p>
<b>G</b>: patch is available but not relevant for your installation
(concerns packages which are not installed).
You can download the patch but it will not be installed.
</p>
<p>
<b>_</b>: you have deselected this patch, it will neither
downloaded nor installed.
</p>"));

    help_text = help_part1 + help_part2 + help_part3;
    
    // using SetContents (define in online_update.ycp)
    UI(`SetWizardContents(_("List of available patches"), contents, help_text, Args(0), Args(1) ));

      
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    ////  Loop for User Input ....
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    symbol ret = `next;

    repeat {

	ret= UI(`UserInput());

	if ( ret == `abort && UI(`ConfirmAbortUpdate(`unusable)) )
	{
	    PKGINFO(`ftpDisconnect());
	    return `abort;
	}
	if (ret == `patch_table || ret == `apply)
	{
	    any id = UI(`QueryWidget(`id(`patch_table), `CurrentItem ));
	    term  patch_info = UI( `QueryWidget(`id(`patch_table), `Item(id)) );
	    y2debug("PATHCHINFO: %1", patch_info);

	    string patch_name = select(patch_info, 3);
	    y2debug("Status of selected patch %1: %2",  patch_name, select( lookup( all_patches, patch_name, []), 3 ));

	    if ( select( patch_info, 1 ) == " X " || select( patch_info, 1 ) == " G " )
	    {
		// mark patch deselected
		UpdateGetPatches( patch_name, " " );
	    }
	    else if ( select( lookup( all_patches, patch_name, []), 3 ) == "X" )
	    {
                // was "X" before -> mark with "X" again
		UpdateGetPatches( patch_name, "X" ); 
	    }
	    else if ( select( lookup( all_patches, patch_name, []), 3 ) == "G" )
	    {
		// was "G" before -> mark with "G" again
		UpdateGetPatches( patch_name, "G" ); 
	    }
	}
	else if ( ret == `info )
	{
	    string patch = UI(`QueryWidget(`id(`patch_table), `CurrentItem ) );
	    ShowPatchInfo( patch );
	}
	else if (ret == `next)
	{
	    y2debug("ONLINE: get patches: %1", get_patches );

	    // write patch info to user_settings
	    user_settings = add(user_settings, "ftp_patches", get_patches);
	}

	if ( ret == `back)
	{

	}

    } until (ret == `next || ret == `back || ret == `cancel || ret == `abort);
    
   return ret;
}



